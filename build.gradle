plugins {
  id 'com.gradleup.shadow' version '9.3.0'
  id 'application'
  id 'jacoco'
  id 'org.sonarqube' version '7.2.2.6593'
  id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'de.komoot.photon'
version = '0.7.0'
description = "Geocoder for OSM data (OpenSearch-based version)"

distZip.enabled = false
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

application {
    mainClass = 'de.komoot.photon.App'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
}

repositories {
     maven { url = uri("https://www.datanucleus.org/downloads/maven2/") }
     mavenCentral()
}

dependencies {
    constraints {
        implementation('commons-io:commons-io') {
            version { require('[2.20.0,)') }
            because 'force latest version to fix CVE-2024-47554'
        }
    }

    implementation 'org.opensearch.client:opensearch-java:3.4.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.5.1'

    implementation('org.codelibs.opensearch:opensearch-runner:3.4.0.0') {
        exclude(module: 'repository-url')
        exclude(module: 'reindex-client')
        exclude(module: 'rank-eval-client')
        exclude(module: 'percolator-client')
        exclude(module: 'parent-join-client')
        exclude(module: 'mapper-extras-client')
        exclude(module: 'opensearch-scripting-painless-spi')
        exclude(module: 'lang-painless')
        exclude(module: 'lang-mustache-client')
        exclude(module: 'lang-expression')
        exclude(module: 'ingest-user-agent')
        exclude(module: 'ingest-common')
        exclude(module: 'aggs-matrix-stats-client')
        exclude(module: 'lucene-grouping')
    }

    implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.3'
    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3'
    implementation 'org.jcommander:jcommander:3.0'
    implementation 'org.springframework:spring-jdbc:7.0.2'
    implementation ('org.apache.commons:commons-dbcp2:2.14.0') {
        exclude(module: 'commons-logging')
    }
    implementation 'org.locationtech.jts:jts-core:1.20.0'
    implementation 'org.locationtech.jts.io:jts-io-common:1.20.0'
    implementation 'io.javalin:javalin:6.7.0'
    implementation 'io.javalin:javalin-micrometer:6.7.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.1'
    implementation 'net.postgis:postgis-jdbc:2025.1.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'

    testImplementation 'com.h2database:h2:2.4.240'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    testImplementation 'org.assertj:assertj-core:3.27.7'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:5.1.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'io.netty') {
            details.useVersion '4.2.8.Final'
            details.because 'force latest version to fix CVE-2025-67735'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    systemProperty "sun.net.http.allowRestrictedHeaders", "true"
}


jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

tasks.named('jar') {
    archiveBaseName.set('original-photon')
    manifest.attributes('Multi-Release': 'true')
}

def gitHashProvider = providers.exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
}.standardOutput.asText.map { it.trim() }

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    failOnDuplicateEntries = true

    mergeServiceFiles()

    filesMatching("META-INF/*") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    filesMatching("META-INF/spring/aot.factories") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    filesMatching("org/apache/commons/logging/**") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    manifest {
      attributes(
        'Implementation-Version': version,
        'Git-Commit': gitHashProvider
      )
    }

    destinationDirectory.set(file('target'))
    archiveBaseName.set('photon')
    archiveClassifier.set('')

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
