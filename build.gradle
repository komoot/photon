plugins {
  id 'com.gradleup.shadow' version '9.2.2'
  id 'application'
  id 'jacoco'
  id 'org.sonarqube' version '6.3.1.5724'
}

group = 'de.komoot.photon'
version = '0.7.0'
description = "Geocoder for OSM data (OpenSearch-based version)"

distZip.enabled = false
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

application {
    mainClass = 'de.komoot.photon.App'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
}

repositories {
     maven { url = uri("https://www.datanucleus.org/downloads/maven2/") }
     mavenCentral()
}

dependencies {
    constraints {
        implementation('commons-io:commons-io') {
            version { require('[2.20.0,)') }
            because 'force latest version to fix CVE-2024-47554'
        }
    }

    implementation 'org.opensearch.client:opensearch-java:3.2.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.5.1'

    implementation('org.codelibs.opensearch:opensearch-runner:3.3.0.0') {
        exclude(module: 'repository-url')
        exclude(module: 'reindex-client')
        exclude(module: 'rank-eval-client')
        exclude(module: 'percolator-client')
        exclude(module: 'parent-join-client')
        exclude(module: 'mapper-extras-client')
        exclude(module: 'opensearch-scripting-painless-spi')
        exclude(module: 'lang-painless')
        exclude(module: 'lang-mustache-client')
        exclude(module: 'lang-expression')
        exclude(module: 'ingest-user-agent')
        exclude(module: 'ingest-common')
        exclude(module: 'aggs-matrix-stats-client')
        exclude(module: 'lucene-grouping')
    }

    implementation 'org.apache.logging.log4j:log4j-core:2.25.2'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.2'
    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.2'
    implementation 'org.jcommander:jcommander:3.0'
    implementation 'org.springframework:spring-jdbc:6.2.12'
    implementation ('org.apache.commons:commons-dbcp2:2.13.0') {
        exclude(module: 'commons-logging')
    }
    implementation 'org.locationtech.jts:jts-core:1.20.0'
    implementation 'org.locationtech.jts.io:jts-io-common:1.20.0'
    implementation 'io.javalin:javalin:6.7.0'
    implementation 'io.javalin:javalin-micrometer:6.7.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.14.5'
    implementation 'net.postgis:postgis-jdbc:2025.1.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'

    testImplementation(platform("org.junit:junit-bom:5.14.0"))
    testImplementation 'com.h2database:h2:2.4.240'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:5.0.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    systemProperty "sun.net.http.allowRestrictedHeaders", "true"
}


jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

tasks.named('jar') {
    archiveBaseName.set('original-photon')
    manifest.attributes('Multi-Release': 'true')
}

def gitHashProvider = providers.exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
}.standardOutput.asText.map { it.trim() }

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    failOnDuplicateEntries = true

    mergeServiceFiles()

    filesMatching("META-INF/*") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    filesMatching("META-INF/spring/aot.factories") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    filesMatching("org/apache/commons/logging/**") {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    manifest {
      attributes(
        'Implementation-Version': version,
        'Git-Commit': gitHashProvider
      )
    }

    destinationDirectory.set(file('target'))
    archiveBaseName.set('photon')
    archiveClassifier.set('')

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)

    // This mitigates against the log4j JNDI lookup vulnerability:
    // https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2021-44228
    // Since we are using an old version of elastic search that is incompatible
    // with a patched, newer version of log4j we have to remove the class
    // JndiLookup from the fat jar. This is the recommended course of action
    // when you cannot upgrade as per https://logging.apache.org/log4j/2.x/security.html
    exclude 'org/apache/logging/log4j/core/lookup/JndiLookup.class'
}
